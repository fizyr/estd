add_library(${PROJECT_NAME}_test_main EXCLUDE_FROM_ALL main.cpp)

if (NOT TARGET tests)
	add_custom_target(tests)
endif()

if (NOT TARGET run_tests)
	add_custom_target(run_tests)
endif()

function(declare_test name)
	add_executable(${name} EXCLUDE_FROM_ALL ${ARGN})
	target_link_libraries(${name} PRIVATE ${PROJECT_NAME}_test_main)
	add_custom_target(run_${prefix}${test} COMMAND ${name})
	add_dependencies(tests ${name})
	add_dependencies(run_tests run_${name})
	add_test(NAME ${name} COMMAND ${name})
endfunction()

function(declare_tests prefix)
	foreach(test ${ARGN})
		declare_test(${prefix}${test} ${test}.cpp)
	endforeach()
endfunction()

function(declare_compile_test name)
	add_library(${name} EXCLUDE_FROM_ALL ${ARGN})
	add_dependencies(tests ${name})
	add_dependencies(run_tests ${name})
endfunction()

function(declare_compile_tests prefix)
	foreach(test ${ARGN})
		declare_compile_test(${prefix}${test} ${test}.cpp)
	endforeach()
endfunction()

add_subdirectory(convert)
add_subdirectory(range)
add_subdirectory(result)
add_subdirectory(traits)
add_subdirectory(tuple)
add_subdirectory(utility)
